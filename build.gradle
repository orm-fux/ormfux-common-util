apply plugin: 'eclipse'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

/* Java version */
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

/* Identifier */
group = 'com.github.ormfux.common'
archivesBaseName = 'common-utils'
version = '1.5'

/* dependencies */
dependencies {
	testImplementation 'org.apache.commons:commons-lang3:3.7'
	testImplementation 'junit:junit:4.11'
	testImplementation 'org.assertj:assertj-core:3.12.0'
}

/* unit test resources folder */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/java/test"
}

processTestResources.dependsOn copyTestResources

/* test logging */
test {
    testLogging {
        exceptionFormat = 'full'
    }
}

		
/* repos */
repositories {
	mavenCentral()
	mavenLocal()
}

/* Maven artifact creation */
task javadocJar(type: Jar) {
	from javadoc
	classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourcesJar
			artifact javadocJar

			afterEvaluate {
				artifactId = 'common-utils'
			}
		}
	}
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}


